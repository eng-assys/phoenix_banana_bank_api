defmodule BananaBank.Users.User do
  use Ecto.Schema

  import Ecto.Changeset

  alias Ecto.Changeset

  @required_params_complete [:name, :password, :email, :zip_code]
  @required_params_update [:name, :email, :zip_code]

  @derive {Jason.Encoder, except: [:__meta__, :password, :password_hash]}
  schema "users" do
    field :name, :string
    field :password, :string, virtual: true
    field :password_hash, :string
    field :email, :string
    field :zip_code, :string

    timestamps()
  end

  def changeset(params) do
    %__MODULE__{}
    |> cast(params, @required_params_complete)
    |> do_validations(@required_params_complete)
    |> add_password_hash()
  end

  def changeset(user, params) do
    user
    |> cast(params, @required_params_complete)
    |> do_validations(@required_params_update)
    |> add_password_hash()
  end

  defp do_validations(changeset, fields) do
    changeset
    |> validate_required(fields)
    |> validate_length(:name, min: 3)
    |> validate_length(:zip_code, is: 8)
    |> validate_format(:email, ~r/@/)
  end

  defp add_password_hash(%Changeset{valid?: true, changes: %{password: password}} = changeset) do
    put_change(changeset, :password_hash, Argon2.hash_pwd_salt(password))
  end

  defp add_password_hash(changeset), do: changeset
end
